module fr.cea.nabla.GenerateNabla

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "fr.cea.nabla"
				rootPath = rootPath
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "fr.cea.nabla.Nabla"
			fileExtensions = "nabla"

			serializer = {
				generateStub = false
			}
			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			
			scopeProvider = scoping.ImportNamespacesScopingFragment2 auto-inject { }
			fragment = exporting.QualifiedNamesFragment2 auto-inject { }
		}
	}
}
